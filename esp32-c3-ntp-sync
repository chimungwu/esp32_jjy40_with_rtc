/*
  JJY Transmitter (ver.0.11) - ESP32-C3 SuperMini / NodeMCU-32S Edition
  ---------------------------------------------------------------------
  Based on: ver.0.10 (2024/05/16 by Nash Shuji009)
  Modified by: chimungwu (2025/05/08)

  Target Board: ESP32-C3 SuperMini（預設設定）
  JJY Output: GPIO6 (PWM 40kHz)
  Status LED: GPIO8（關閉：未發送，閃爍：發送中）

  ✅ 若使用 ESP32 NodeMCU-32S，請依下列方式修改腳位與狀態顯示：
     - 將 PWM 輸出腳位改為 GPIO26： const int ledPin = 26;
     - 將狀態 LED 腳位改為 GPIO2： #define STATUS_LED 2，或是依需求修改
  ✅JJY 發波線圈：接至 GPIO6，並串聯一顆 220Ω 電阻後再接到線圈。
  ✅狀態 LED：可使用板載 LED（GPIO2），或外接 LED（串聯 220Ω 後接 GND）。

  == 功能說明 ==
  ✅ 每次開機都進入 WiFiManager「JJY-Setup」設定模式，讓使用者設定 Wi-Fi 與時區
  ✅ 使用者可在設定頁輸入時區（格式如 +8 或 +9），支援自訂 NTP 時區校時
  ✅ 成功連線 Wi-Fi 並設定後，會自動同步 NTP 並開始發送模擬 JJY 時碼
  ✅ 發波時間與日本 JJY 電波對齊（整分鐘開始），每秒傳送一次時間碼
  ✅ 支援閏秒與錯誤值檢查，發送結束後序列埠會輸出傳送內容

  == 指示燈狀態 ==
  - 快速閃爍：尚未完成 NTP 對時
  - 每秒閃爍1次：NTP 對時成功，已開始傳送 JJY 訊號

  == 相依函式庫 ==
  - WiFiManager by tzapu: https://github.com/tzapu/WiFiManager
  - Preferences（儲存時區）

  📌 時區預設值為 UTC+9（日本時間）
  ➜ 若需改為台灣時間，設定頁請輸入 +8
*/


#include <Arduino.h>
#include <driver/ledc.h>
#include <WiFi.h>
#include <WiFiManager.h>
#include <Preferences.h>

// === Hardware Settings ===
const int ledChannel = 0;  // PWM channel for JJY signal output
const int ledPin     = 6;  // PWM output pin (connected to JJY antenna via GPIO6)
#define STATUS_LED    8    // Onboard status LED (GPIO8 on ESP32-C3 SuperMini)

// === JJY Signal Variables ===
char P0, P1, P2, P3, P4, P5;
const char M = P0 = P1 = P2 = P3 = P4 = P5 = -1;  // Marker signal (-1 or 255)
char PA1, PA2;
char SU1, LS1, LS2;
char sg[60];

// === Time Sync Status ===
bool ntpSynced       = true;
unsigned long lastBlink = 0;
bool ledState        = false;

// === Timezone Configuration ===
Preferences prefs;
String tzString = "+9";
long timezoneOffsetSec = 9 * 3600L;

void setup() {
  Serial.begin(115200);
  delay(1000);

  pinMode(STATUS_LED, OUTPUT);
  digitalWrite(STATUS_LED, LOW);

  set_fix();
  ledcSetup(ledChannel, 40000, 8);
  ledcAttachPin(ledPin, ledChannel);

  WiFiManager wm;
  wm.resetSettings();  // <== 強制清除
  WiFiManagerParameter tzParam("tz", "Enter timezone (e.g. +8 for Taiwan, default +9 for Japan)", "+9", 4);
  wm.addParameter(&tzParam);

  if (!wm.autoConnect("JJY-Setup")) {
    Serial.println("\n❌ WiFiManager 連線失敗，無法進行 NTP 同步");
    ntpSynced = false;
    return;
  }

  Serial.println("\n📶 WiFiManager 成功連線");

  tzString = String(tzParam.getValue());
  prefs.begin("jjy", false);
  prefs.putString("tz", tzString);
  prefs.end();

  int tz_hour = tzString.toInt();
  timezoneOffsetSec = tz_hour * 3600L;
  Serial.printf("🌐 Timezone 設定為 UTC%+d\n", tz_hour);

  configTime(timezoneOffsetSec, 0,
             "time.stdtime.gov.tw", "time.google.com", "ntp.twnic.tw");

  struct tm timeInfo;
  int retries = 0;

  Serial.print("⏳ Synchronizing time via NTP");
  while (!getLocalTime(&timeInfo) && retries < 10) {
    delay(500);
    Serial.print(".");
    retries++;
  }

  if (getLocalTime(&timeInfo)) {
    ntpSynced = true;
    Serial.println("\n✅ NTP time synchronized successfully.");

    char timeStr[64];
    sprintf(timeStr, "🕒 Current Time: %04d-%02d-%02d %02d:%02d:%02d",
            timeInfo.tm_year + 1900, timeInfo.tm_mon + 1, timeInfo.tm_mday,
            timeInfo.tm_hour, timeInfo.tm_min, timeInfo.tm_sec);
    Serial.println(timeStr);
  } else {
    Serial.println("\n❌ Failed to synchronize time via NTP.");
    ntpSynced = false;
  }
}


void delayUntilAlignedRTCWrite() {
  struct timeval tv;
  gettimeofday(&tv, NULL);

  int64_t micros_now = tv.tv_sec * 1000000LL + tv.tv_usec;
  int64_t micros_next_minute = ((tv.tv_sec / 60) + 1) * 60 * 1000000LL;

  int64_t delay_us = micros_next_minute - micros_now;

  Serial.printf("⏱ 對齊延遲 %.3f 秒...\n", delay_us / 1000000.0);

  if (delay_us >= 1000) {
    delay(delay_us / 1000);                  // 毫秒以上部分
    delayMicroseconds(delay_us % 1000);      // 剩下微秒
  } else {
    delayMicroseconds(delay_us);
  }
}

bool hasAligned = false;  // ⏱ 只在第一次對齊

void loop() {
  // 狀態燈邏輯：同步成功 → 常亮；未同步 → 每秒閃爍
  if (!ntpSynced) {
    if (millis() - lastBlink >= 500) {
      ledState = !ledState;
      digitalWrite(STATUS_LED, ledState ? HIGH : LOW);
      lastBlink = millis();
    }
  }

  // 時碼發送邏輯
  if (ntpSynced) {
    if (!hasAligned) {
      delayUntilAlignedRTCWrite();  // ⏱ 第一次進入時對齊整分鐘
      hasAligned = true;
    }
    printAndSendJJY();  // 發送完整一分鐘的 JJY 時碼
  } else {
    delay(1000);  // 尚未同步時，延遲等待 WiFi/NTP 重啟
  }
}


void set_year(int n){
  
  int m = dec2BCD(n);

  sg[48] = m % 2; m = m >> 1;
  sg[47] = m % 2; m = m >> 1;
  sg[46] = m % 2; m = m >> 1;
  sg[45] = m % 2; m = m >> 1;

  sg[44] = m % 2; m = m >> 1;
  sg[43] = m % 2; m = m >> 1;
  sg[42] = m % 2; m = m >> 1;
  sg[41] = m % 2;
}

void printAndSendJJY() {
  struct tm timeInfo;
  char s[40];
  char t[64];
  int se, mi, sh;

  getLocalTime(&timeInfo);

  sprintf(s, "%04d/%02d/%02d %02d:%02d:%02d",
          timeInfo.tm_year + 1900, timeInfo.tm_mon + 1, timeInfo.tm_mday,
          timeInfo.tm_hour, timeInfo.tm_min, timeInfo.tm_sec);
  Serial.println(s);

  sprintf(t, "Y:%02d  D:%03d WD:%01d",
          timeInfo.tm_year + 1900 - 2000, timeInfo.tm_yday + 1, timeInfo.tm_wday);
  Serial.println(t);

  // 閏秒補正
  se = timeInfo.tm_sec;
  sh = 0;
  if (timeInfo.tm_sec == 60) {
    sg[53] = LS1 = 1;
    sg[54] = LS2 = 0;
    se = timeInfo.tm_sec - 1;
    sh = 1;
  }
  if (timeInfo.tm_sec == 61) {
    sg[53] = LS1 = 1;
    sg[54] = LS2 = 1;
    se = timeInfo.tm_sec - 2;
    sh = 2;
  }

  set_min(timeInfo.tm_min);
  set_hour(timeInfo.tm_hour);
  set_day(timeInfo.tm_yday + 1);
  set_wday(timeInfo.tm_wday);
  set_year(timeInfo.tm_year + 1900 - 2000);

  for (int i = se; i < 60 + sh; i++) {

    // 狀態燈閃爍（每秒）
    if (millis() - lastBlink >= 1000) {
      ledState = !ledState;
      digitalWrite(STATUS_LED, ledState ? HIGH : LOW);
      lastBlink = millis();
    }

    sprintf(t, "%02d ", sg[i]);
    Serial.print(t);
    switch (sg[i]) {
      case -1:
      case 255:
        mark();
        break;
      case 0:
        zero();
        break;
      case 1:
        one();
        break;
      default:
        zero();
        sprintf(t, "%02d  %02d", i, sg[i]);
        Serial.print("***BUG data ***  ");
        Serial.println(t);
        break;
    }
  }
}

void set_day(int n){
  
  int m = dec2BCD(n);

  sg[33] = m % 2; m = m >> 1;
  sg[32] = m % 2; m = m >> 1;
  sg[31] = m % 2; m = m >> 1;
  sg[30] = m % 2; m = m >> 1;

  sg[28] = m % 2; m = m >> 1;
  sg[27] = m % 2; m = m >> 1;
  sg[26] = m % 2; m = m >> 1;
  sg[25] = m % 2; m = m >> 1;

  sg[23] = m % 2; m = m >> 1;
  sg[22] = m % 2;
}

void set_wday(int m){

  sg[52] = m % 2; m = m >> 1;
  sg[51] = m % 2; m = m >> 1;
  sg[50] = m % 2;
}

void set_hour(int n){
  
  int m = dec2BCD(n);

  sg[18] = m % 2; m = m >> 1;
  sg[17] = m % 2; m = m >> 1;
  sg[16] = m % 2; m = m >> 1;
  sg[15] = m % 2; m = m >> 1;
  
  sg[13] = m % 2; m = m >> 1;
  sg[12] = m % 2;
  
  char PA1 = sg[18] ^ sg[17] ^ sg[16] ^ sg[15] ^ sg[13] ^ sg[12]; //PA1 = (20h+10h+8h+4h+2h+1h) mod 2
  sg[36] = PA1;
}

void set_min(int n){
  
  int m = dec2BCD(n);


  sg[8] = m % 2; m = m >> 1;
  sg[7] = m % 2; m = m >> 1;
  sg[6] = m % 2; m = m >> 1;
  sg[5] = m % 2; m = m >> 1;

  sg[3] = m % 2; m = m >> 1;
  sg[2] = m % 2; m = m >> 1;
  sg[1] = m % 2;

  char PA2 = sg[8] ^ sg[7] ^ sg[6] ^ sg[5] ^ sg[3] ^ sg[2] ^ sg[1]; //PA2 = (40m+20m+10m+8m+4m+2m+1m) mod 2
  sg[37] = PA2;
}


void set_fix(){
  sg[0] = sg[9] = sg[19] = sg[29] = sg[39] = sg[49] = sg[59] = M;
  sg[4] = sg[10] = sg[11] = sg[14] = sg[20] = sg[21] = sg[24] = sg[34] = sg[35] = sg[55] = sg[56] = sg[57] = sg[58] = 0;
  sg[38] = sg[40] = 0;
  sg[53] = sg[54] = 0;
  
}

int dec2BCD(int decimal) {
    int bcd = 0;
    int multiplier = 1;

    while (decimal > 0) {
        int digit = decimal % 10;
        bcd += digit * multiplier;
        multiplier *= 16; // 10進数の各桁を4ビットのBCDに変換するために16倍する
        decimal /= 10;
    }

    return bcd;
    
    ///printf("BCD: %X\n", bcd);
}


void mark() {   // 0.2sec
  ledcWrite(ledChannel, 127);
  delay(200);
  ledcWrite(ledChannel, 0);
  delay(800);
}

void zero() {  // 0.8sec
  ledcWrite(ledChannel, 127);
  delay(800);
  ledcWrite(ledChannel, 0);
  delay(200);
}

void one() {    // 0.5sec
  ledcWrite(ledChannel, 127);
  delay(500);
  ledcWrite(ledChannel, 0);
  delay(500);
}
